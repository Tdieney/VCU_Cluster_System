33# Systemd startup script

Setup a custom systemd service in Yocto to auto-connect to Wi-Fi and enable CAN at boot using a shell script and BitBake recipe.

```
meta-tdieney/
└── recipes-core/
    └── startup/
        ├── files/
        │   ├── mystartup.sh
        │   └── my-startup.service
        └── startup_1.0.bb
```

---

## 1. mystartup.sh (scripts/files/mystartup.sh)

```sh
#!/bin/sh
echo "[$(date '+%Y-%m-%d %H:%M:%S')] Starting custom startup script" >> /var/log/startup.log

# Wait until wifi network is detected
SSID="Wifi_Name"
TIMEOUT=20
FOUND=0

for i in $(seq 1 $TIMEOUT); do
    if nmcli -t -f SSID dev wifi | grep -Fxq "$SSID"; then
        FOUND=1
        break
    fi
    echo "[$(date '+%H:%M:%S')] Waiting for SSID '$SSID'..." >> /var/log/startup.log
    sleep 2
done

if [ "$FOUND" -eq 1 ]; then
    echo "SSID found, connecting..." >> /var/log/startup.log
    nmcli device wifi connect "$SSID" password "Wifi_Password"
    nmcli connection modify "$SSID" connection.autoconnect yes
else
    echo "ERROR: SSID '$SSID' not found after $((TIMEOUT*2))s" >> /var/log/startup.log
fi

# Bring up can0 at 1Mbps
ip link set can0 up type can bitrate 1000000

# Setup for QT
QT_QPA_PLATFORM=wayland XDG_RUNTIME_DIR=/tmp/runtime-root /usr/bin/qtapp
```

Make it executable: 

```
chmod +x mystartup.sh
```

At boot time, the script runs quite early, before `NetworkManager` has scanned and listed available Wi-Fi networks.  
So `nmcli device wifi connect` fails because SSID not found yet. So, add a short delay + loop until Wi-Fi appears

---

## 2. my-startup.service (scripts/files/my-startup.service)

```ini
[Unit]
Description=Custom startup commands
After=network.target network-online.target

[Service]
Type=oneshot
ExecStart=/usr/bin/mystartup.sh
RemainAfterExit=true

[Install]
WantedBy=multi-user.target
```

### `[Unit]` section

This tells systemd **when** and **why** to run the service.

```ini
Description=Custom startup commands
```

* A human-readable name for the service (shows in `systemctl status`).

```ini
After=network.target network-online.target
```

* This tells systemd: **“Wait until the network is ready before starting this service.”**
* Useful if your script needs internet, Wi-Fi, or IP address set up.

---

### `[Service]` section

This defines **how the script should run**.

```ini
Type=oneshot
```

* This means the service runs **once and exits** (not long-running).
* Good for one-time setup tasks or command scripts.

```ini
ExecStart=/usr/bin/mystartup.sh
```

* This is the **script to run**.
* Make sure `/usr/bin/mystartup.sh` exists and is **executable** (`chmod +x`).

```ini
RemainAfterExit=true
```

* Tells systemd to **consider the service still “active”** even after the script ends.
* Helps you see the service as “active” when you run `systemctl status`.

---

### `[Install]` section

This controls **when the service should be started**.

```ini
WantedBy=multi-user.target
```

* Says: “Run this service during the normal boot process (multi-user mode).”
* This is the **standard target** for most services on headless or server-style systems.

---

## 3. BitBake recipe: startup\_1.0.bb (recipes-core/startup/startup\_1.0.bb)

```bitbake
DESCRIPTION = "Custom startup script and systemd service"
LICENSE = "CLOSED"
PRIORITY = "optional"

SRC_URI += "file://my-startup.service \
            file://mystartup.sh"

inherit systemd

S = "${WORKDIR}"

do_install() {
    # install script
    install -d ${D}${bindir}
    install -m 0755 ${WORKDIR}/mystartup.sh ${D}${bindir}/mystartup.sh

    # install systemd unit
    install -d ${D}${systemd_system_unitdir}
    install -m 0644 ${WORKDIR}/my-startup.service ${D}${systemd_system_unitdir}/my-startup.service
}

FILES:${PN} += "${systemd_system_unitdir}/my-startup.service"

# Tell systemd class the unit to enable
SYSTEMD_SERVICE:${PN} = "my-startup.service"

# Ensure service is enabled in image
SYSTEMD_AUTO_ENABLE = "enable"

```

This recipe installs a custom shell script and sets it up to run automatically at boot using `systemd`.

### Line-by-line breakdown

```bitbake
DESCRIPTION = "Custom startup script and systemd service"
LICENSE = "CLOSED"
PRIORITY = "optional"
```

* Metadata: description, license, priority of the recipe.

```bitbake
SRC_URI += "file://my-startup.service \
            file://mystartup.sh"
```

* Declares source files that should be included (both the script and the service file).

```bitbake
inherit systemd
```

* Inherits Yocto's built-in `systemd` class to handle installation and service enabling.

```bitbake
S = "${WORKDIR}"
```

* Sets the working source directory (not using a separate unpack dir here).

```bitbake
do_install() {
    # install script
    install -d ${D}${bindir}
    install -m 0755 ${WORKDIR}/mystartup.sh ${D}${bindir}/mystartup.sh

    # install systemd unit
    install -d ${D}${systemd_system_unitdir}
    install -m 0644 ${WORKDIR}/my-startup.service ${D}${systemd_system_unitdir}/my-startup.service
}
```

* Installs:
  * Your script to `/usr/bin/`
  * Your `.service` file to `/lib/systemd/system/` (via `systemd_system_unitdir`)

```bitbake
FILES:${PN} += "${systemd_system_unitdir}/my-startup.service"
```

* Ensures the `.service` file is included in the final package image.

```bitbake
SYSTEMD_SERVICE:${PN} = "my-startup.service"
SYSTEMD_AUTO_ENABLE = "enable"
```

* Tells Yocto to **enable** this service automatically at boot.

---

## 4. Add to IMAGE\_INSTALL

In your image recipe or local.conf:

```bitbake
IMAGE_INSTALL:append = " networkmanager"
IMAGE_INSTALL:append = " startup"

INIT_MANAGER = "systemd"
```

Then rebuild your image

```bash
bitbake core-image-base
```

Upon first boot, systemd will enable and run the script automatically.
